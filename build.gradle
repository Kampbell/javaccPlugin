apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'checkstyle'
apply plugin: 'findbugs'
apply plugin: 'jacoco'
apply plugin: 'maven'
apply plugin: 'signing'

sourceCompatibility = 1.5
targetCompatibility = 1.5
group = 'ca.coglinc'
version = '1.0.0'

repositories {
    mavenCentral()
}

dependencies {
    compile gradleApi()
    compile group: 'net.java.dev.javacc', name: 'javacc', version: '5.0'
     
    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile group: 'org.mockito', name: 'mockito-all', version: '1.9.5'
}

eclipse {
    project {    
        name = 'javacc-gradle-plugin'
    }
      
    classpath {
        defaultOutputDir = file('build-eclipse')
        downloadSources = true
        downloadJavadoc = true
    }
}

compileJava {
    options.encoding = 'UTF-8'
}

compileTestJava {
    options.encoding = 'UTF-8'
}

jar {
    manifest {
        attributes 'Implementation-Title': 'javacc-gradle-plugin', 'Implementation-Version': version
    }
}

checkstyle {
    configFile = new File("${project.projectDir}/config/checkstyle/checks.xml")
}

findbugs {
    sourceSets = [sourceSets.main, sourceSets.test]
    effort = "min"
    ignoreFailures = true
    reportLevel = "high"
    excludeFilter = file("${project.projectDir}/config/findbugs/exclude.xml")
}

tasks.withType(FindBugs) {
    maxHeapSize = project.hasProperty('findBugsHeapSize') ? project.findBugsHeapSize : '1g'
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled true
        csv.enabled false
    }
}

task sourceJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn:javadoc) { 
     classifier = 'javadoc' 
     from javadoc.destinationDir 
} 

artifacts {
    archives sourceJar
    archives javadocJar
}

signing {
    sign configurations.archives
}

uploadArchives {  
    repositories {  
        mavenDeployer {  
            beforeDeployment {
                MavenDeployment deployment -> signing.signPom(deployment)
            }
            
            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }
        
            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }
            
            pom.project {
                packaging 'jar'
                name 'javacc-gradle-plugin'
                description 'Provides the ability to use JavaCC with Gradle. Compiles JavaCC files to Java.'
                url 'https://github.com/johnmartel/javaccPlugin'
                licenses {
                    license {
                        name 'The MIT License (MIT)'
                        url 'http://opensource.org/licenses/MIT'
                        distribution 'repo'
                    }
                }
                scm {
                    url 'https://github.com/johnmartel/javaccPlugin'
                    connection 'scm:https://johnmartel@github.com/johnmartel/javaccPlugin.git'
                    developerConnection 'scm:git://github.com/johnmartel/javaccPlugin.git' 
                }
                developers {
                    developer {
                        id 'jmartel'
                        name 'Jonathan Martel'
                        email 'jonathan.martel@coglinc.ca'
                    }
                }
            }
        }  
    }  
}
