plugins {
    id 'checkstyle'
    id 'findbugs'
}

defaultTasks 'clean', 'build'

configurations.all {
    resolutionStrategy {
        exclude group: 'commons-lang'
    }
}

dependencies {
    testRuntime project(':javacc-gradle-plugin')
    testRuntime files(project(':javacc-gradle-plugin').createClasspathManifest)

    testCompile('commons-io:commons-io:2.4') {
        force = true
    }
    testCompile 'org.apache.commons:commons-lang3:3.4'
    testCompile 'junit:junit:4.12'

    testCompile gradleTestKit()
}

eclipse {
    project {
        name = 'javacc-gradle-plugin-acceptance-tests'
    }
}

jar.enabled = false

checkstyle {
    configFile = file("${rootProject.projectDir}/config/checkstyle/checks.xml")
}

findbugs {
    toolVersion = '3.0.1'
    sourceSets = [sourceSets.main, sourceSets.test]
    effort = "max"
    ignoreFailures = false
    reportLevel = "low"
    excludeFilter = file("${rootProject.projectDir}/config/findbugs/exclude.xml")
}

tasks.withType(FindBugs) {
    maxHeapSize = project.hasProperty('findBugsHeapSize') ? project.findBugsHeapSize : '1g'
    
    reports {
        xml.enabled false
        html.enabled true
    }
}

task testJar(type: Jar, dependsOn: testClasses) {
    classifier 'tests'
    from sourceSets.test.output
}

artifacts {
    archives testJar
}
