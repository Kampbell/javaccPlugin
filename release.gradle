apply from: 'build.gradle'

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.ajoberstar:gradle-git:0.9.0'
    }
}

import org.ajoberstar.grgit.Grgit

defaultTasks 'release'

def releaseVersionProperty = 'releaseVersion'
def nextVersionProperty = 'nextVersion'

ext.gitRepo = Grgit.open(projectDir)

task verifyReleaseVersions {
    description "Verifies that the [${releaseVersionProperty}] and [${nextVersionProperty}] project properties are specified"
    group 'Release'
}

verifyReleaseVersions << {
    if (!project.hasProperty(releaseVersionProperty) || !project.hasProperty(nextVersionProperty)) {
        throw new RuntimeException("[${releaseVersionProperty}] and [${nextVersionProperty}] project properties are mandatory.")
    }
}

task verifySnapshotDependencies {
    description 'Fails the build if there are SNAPSHOT dependencies.'
    group 'Release'
}

verifySnapshotDependencies << {
    configurations.each { configuration ->
        configuration.dependencies.each { dependency ->
            logger.info "Verifying [${configuration.name}] dependency [${dependency.group}]:[${dependency.name}]:[${dependency.version}]"
            
            if (dependency.version != null && dependency.version.endsWith('SNAPSHOT')) {
                throw new RuntimeException("Plugin can not be released with SNAPSHOT dependencies. See [${configuration.name}] [${dependency.group}]:[${dependency.name}]:[${dependency.version}]")
            }
        }
    }
}

task checkoutBranch {
    description 'Checks out the branch before modifying any file so that they can be committed afterwards by Jenkins'
    group 'Release'
}

checkoutBranch << {
    gitRepo.checkout {
        branch = "${project.gitBranch}"
        createBranch = false
    }
}

build.mustRunAfter clean, verifyReleaseVersions, verifySnapshotDependencies, checkoutBranch

def commitChangedFiles(commitMessage) {
    gitRepo.commit {
        message = commitMessage
        all = true
    }
    gitRepo.push {
        all = false
        remote = "${project.scmUrl}"
    }
}

task generateReadmeFileFromTemplate(type: Copy, dependsOn: verifyReleaseVersions) {
    description 'Generates the README.md file from a template.'
    group 'Release'
    
    from 'buildSrc'
    into projectDir
    include 'README.md.template'
    rename '(.*)\\.template', '$1'
    expand project.properties
}

generateReadmeFileFromTemplate << {
    commitChangedFiles("Generated README file for version ${releaseVersion}")
}

task writeReleaseVersion(type: Copy, dependsOn: verifyReleaseVersions) {
    description "Writes the value of [${releaseVersionProperty}] project property to build.gradle as the project version."
    group 'Release'
    
    from projectDir
    into buildDir
    include 'build.gradle'
    filter { line ->
        if (line.startsWith('version =')) {
            "version = '${project.getProperty(releaseVersionProperty)}'"
        } else {
            "${line}"
        }
    }
}

writeReleaseVersion.mustRunAfter build

task copyReleaseBuildFile(type: Copy, dependsOn: writeReleaseVersion) {
    description 'Overwrites build.gradle with the release version value.'
    group 'Release'

    from buildDir
    into projectDir
    include 'build.gradle'
}

copyReleaseBuildFile << {
    commitChangedFiles("Updated project version for release ${releaseVersion}")
}

task tagRelease {
    description 'Creates a tag in SCM for the release.'
    group 'Release'
}

tagRelease << {
    gitRepo.tag.add {
        name = "javacc-gradle-plugin-${releaseVersion}"
        annotate = false
    }
    gitRepo.push {
        all = false
        remote = "${project.scmUrl}"
        tags = true
    }
}

tagRelease.mustRunAfter generateReadmeFileFromTemplate, copyReleaseBuildFile

task writeNextVersion(type: Copy, dependsOn: verifyReleaseVersions) {
    description "Writes the value of [${nextVersionProperty}] project property to build.gradle as the project version."
    group 'Release'
    
    from projectDir
    into buildDir
    include 'build.gradle'
    filter { line ->
        if (line.startsWith('version =')) {
            "version = '${project.getProperty(nextVersionProperty)}'"
        } else {
            "${line}"
        }
    }
}

writeNextVersion.mustRunAfter build, tagRelease

task copyNextBuildFile(type: Copy, dependsOn: writeNextVersion) {
    description 'Overwrites build.gradle with the next version value.'
    group 'Release'

    from buildDir
    into projectDir
    include 'build.gradle'
}

copyNextBuildFile << {
    commitChangedFiles("Updated project version for next release ${nextVersion}")
}

task release(dependsOn: [clean, verifyReleaseVersions, verifySnapshotDependencies, checkoutBranch, build, generateReadmeFileFromTemplate, copyReleaseBuildFile, tagRelease, copyNextBuildFile]) {
    description "Releases the plugin by verifying there are no snapshot dependencies, writing the next version to build.gradle and committing the build file. " +
        "Versions must be specified by using the [${releaseVersionProperty}] and [${nextVersionProperty}] project properties."
    
    group 'Release'
    
    if (project.hasProperty(releaseVersionProperty)) {
        version = project.getProperty(releaseVersionProperty)
    }
    println "Changing project's version to [${version}]"
}
    